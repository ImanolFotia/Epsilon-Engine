#version 430 core

layout(location = 0) out vec3 gAlbedoSpec;
layout(location = 1) out float gPosition;
layout(location = 2) out vec4 gExpensiveNormal;
layout(location = 3) out vec4 gDepth;
layout(location = 4) out vec4 gExtraComponents; 
layout(location = 5) out vec3 gLightAccumulation;

layout (binding = 0) uniform sampler2D texture_diffuse;
layout (binding = 1) uniform sampler2D texture_specular;
layout (binding = 2) uniform sampler2D texture_normal;
layout (binding = 3) uniform sampler2D texture_height;
layout (binding = 4) uniform samplerCube skybox;

in VS_OUT{
  vec3 FragPos;
  vec3 Normal;
  vec2 TexCoords;
  mat3 TBN;
  mat3 TBN2;
  vec3 wFragPos;
} fs_in;


uniform vec3 viewPos;
uniform float time;

uniform mat4 projection;

float NEAR = 0.1;
float FAR = 3000.0;
float LinearizeDepth(float depth)
{
    float z = depth * 2.0 - 1.0; // Back to NDC
    return ((2.0 * NEAR * FAR) / (FAR + NEAR - z * (FAR - NEAR)));
}

float deLinearizeDepth(float depth){
    float nonLinearDepth = (FAR + NEAR - 2.0 * NEAR * FAR / depth) / (FAR - NEAR);
    nonLinearDepth = (nonLinearDepth + 1.0) / 2.0;
    return nonLinearDepth;
}

in vec4 PrevPosition;
in vec4 Position;


const float Pi = 3.141592654f;
const float CosineA0 = Pi;
const float CosineA1 = (2.0f * Pi) / 3.0f;
const float CosineA2 = Pi * 0.25f;
// scale for restored amount of lighting
uniform float u_scaleFactor = 0.5;

// coefficients of spherical harmonics and possible values
vec3 u_L00 = vec3(0.00069917, 0.000636377, 0.00103519);
vec3 u_L1m1 = vec3(0.000158042, 0.000210175, 0.000571624);
vec3 u_L10 = vec3(-9.60469e-007, -1.90034e-006, -8.98426e-007);
vec3 u_L11 = vec3(-7.90908e-006, -2.77222e-005, -9.7246e-005);
vec3 u_L2m2 = vec3(-0.000151222, -0.000140365, -0.000256554);
vec3 u_L2m1 = vec3(-2.27325e-006, -2.22188e-006, -4.66022e-006);
vec3 u_L20 = vec3(-0.000128482, -9.65931e-005, -0.000135669);
vec3 u_L21 = vec3(-4.05267e-006, -1.33986e-006, -8.86971e-007);
vec3 u_L22 = vec3(-0.000141542, -0.000116212, -0.000192996);

struct SphericalHarmonicsFormat {
  vec3 u_L00; 
  vec3 u_L1m1;
  vec3 u_L10;
  vec3 u_L11;
  vec3 u_L2m2;
  vec3 u_L2m1;
  vec3 u_L20;
  vec3 u_L21;
  vec3 u_L22;
};

layout(std430, binding = 1) buffer AmbientLightSSBO
{
    //SphericalHarmonicsFormat ambientProbes[];

        vec3 u_L00; float padding0;
        vec3 u_L1m1; float padding1;
        vec3 u_L10; float padding2;
        vec3 u_L11; float padding3;
        vec3 u_L2m2; float padding4;
        vec3 u_L2m1; float padding5;
        vec3 u_L20; float padding6;
        vec3 u_L21; float padding7;
        vec3 u_L22; float padding8;

} lp;

const float C1 = 0.429043;
const float C2 = 0.511664;
const float C3 = 0.743125;
const float C4 = 0.886227;
const float C5 = 0.247708;

vec3 sphericalHarmonics(vec3 N)
{
   return
      // band 0, constant value, details of lowest frequency
      C4 * lp.u_L00 +

      // band 1, oriented along main axes
      2.0 * C2 * lp.u_L11 * N.x +
      2.0 * C2 * lp.u_L1m1 * N.y +
      2.0 * C2 * lp.u_L10 * N.z +

      // band 2, values depend on multiple axes, higher frequency details
      C1 * lp.u_L22 * (N.x * N.x - N.y * N.y) +
      C3 * lp.u_L20 * N.z * N.z - C5 * lp.u_L20 +
      2.0 * C1 * lp.u_L2m2 * N.x * N.y +
      2.0 * C1 * lp.u_L21 * N.x * N.z +
      2.0 * C1 * lp.u_L2m1 * N.y * N.z;
}

int numSteps = 0;
float currentLayerDepth = 0.0;
float weight = 0.0;
float layerDepth = 0.0;
vec2 ParallaxMapping(vec2 texCoords, vec3 viewDir)
{ 
    // number of depth layers
    const float minLayers = 8;
    const float maxLayers = 32;
    float numLayers = mix(maxLayers, minLayers, abs(dot(vec3(0.0, 0.0, 1.0), viewDir)));  
    // calculate the size of each layer
    layerDepth = 1.0 / numLayers;
    // depth of current layer
    // the amount to shift the texture coordinates per layer (from vector P)
    vec2 P = viewDir.xy / viewDir.z * 0.08; 
    vec2 deltaTexCoords = P / numLayers;
  
    // get initial values
    vec2  currentTexCoords     = texCoords;
    float currentDepthMapValue = texture(texture_height, currentTexCoords).b;

    int maxSteps = 16;
      
    while(currentLayerDepth < currentDepthMapValue)
    {
        if(numSteps >= maxSteps)
          break;
        // shift texture coordinates along direction of P
        currentTexCoords -= deltaTexCoords;
        // get depthmap value at current texture coordinates
        currentDepthMapValue = texture(texture_height, currentTexCoords).b;  
        // get depth of next layer
        currentLayerDepth += layerDepth;  

        numSteps++;
    }
    
    // get texture coordinates before collision (reverse operations)
    vec2 prevTexCoords = currentTexCoords + deltaTexCoords;

    // get depth after and before collision for linear interpolation
    float afterDepth  = currentDepthMapValue - currentLayerDepth;
    float beforeDepth = texture(texture_height, prevTexCoords).b - currentLayerDepth + layerDepth;
 
    // interpolation of texture coordinates
    weight = afterDepth / (afterDepth - beforeDepth);
    vec2 finalTexCoords = prevTexCoords * weight + currentTexCoords * (1.0 - weight);

    return finalTexCoords;
}

vec3 bpcem (in vec3 v, vec3 Emax, vec3 Emin, vec3 Epos)
{ 
  vec3 nrdir = normalize(v);
  vec3 rbmax = (Emax - fs_in.wFragPos)/nrdir;
  vec3 rbmin = (Emin - fs_in.wFragPos)/nrdir;
  
  vec3 rbminmax;
  rbminmax.x = (nrdir.x>0.0)?rbmax.x:rbmin.x;
  rbminmax.y = (nrdir.y>0.0)?rbmax.y:rbmin.y;
  rbminmax.z = (nrdir.z>0.0)?rbmax.z:rbmin.z;   
  float fa = min(min(rbminmax.x, rbminmax.y), rbminmax.z);
  vec3 posonbox = fs_in.wFragPos + nrdir * fa;
  return posonbox - Epos;
}

vec3 BoxMax = vec3(40, 25.8, 4);
vec3 BoxMin = vec3(-40,0.8,-40);

void main()
{
  if(texture(texture_diffuse, fs_in.TexCoords).a < 0.9)
    discard;    
    vec3 Dir = normalize((fs_in.TBN * viewPos) - (fs_in.TBN * fs_in.wFragPos));

  vec2 texCoords;  
  if(texture(texture_height, fs_in.TexCoords).b > 0.0)
  texCoords = ParallaxMapping(fs_in.TexCoords, Dir);
  else
  texCoords = fs_in.TexCoords;

  vec3 normalTexture = normalize(texture(texture_normal, texCoords).rgb * 2.0 - 1.0);
  gExpensiveNormal.rgb = normalize(normalTexture * fs_in.TBN);
  //vec4 decalTex = texture(texture_decal, fs_in.TexCoords + (vec2(viewPos.x - 225.0, viewPos.z-300.0)));

  //gAlbedoSpec.a = texture(texture_specular, fs_in.TexCoords).r;
  vec2 Metallic_Emmisive = texture(texture_height, texCoords).rg;
  gExpensiveNormal.a = texture(texture_specular, texCoords).r;

  vec4 CubeMapcolor = vec4(0.0);
  vec3 R = vec3(0.0);
  gAlbedoSpec.rgb = vec3(0.0001);
  gAlbedoSpec.rgb += texture(texture_diffuse,texCoords).rgb;
  
  float Depth = LinearizeDepth(gl_FragCoord.z);
  float bias = max(0.01 * (1.0 - dot(gExpensiveNormal.rgb, Dir)), 0.5) * texture(texture_height, texCoords).b;
  Depth += smoothstep(0.4, 0.0, texture(texture_height, texCoords).b);
  gDepth.r = gl_FragCoord.z;//deLinearizeDepth(Depth);
  //gl_FragDepth = gDepth.r;
  gPosition = fs_in.FragPos.z;
  gExtraComponents.xy = Metallic_Emmisive.xy; //vec4(Metallic_Emmisive, 1.0, 1.0);

  vec2 a = (Position.xy / Position.w) * 0.5 + 0.5;
  vec2 b = (PrevPosition.xy / PrevPosition.w) * 0.5 + 0.5;
  gExtraComponents.zw = (a - b);

  //if( texture( skybox, vec3(0)).x > 0)
  //{
    vec3 I = normalize(fs_in.wFragPos - viewPos);
    R = reflect(I, normalize(gExpensiveNormal.rgb));
    CubeMapcolor = texture(skybox, bpcem(R, BoxMax, BoxMin, vec3(0, 12, 0)));
  //}

  //else
  //  CubeMapcolor = gAlbedoSpec.rgba * 0.0008;

  vec3 schDir =  normalize(gExpensiveNormal.rgb);
  schDir = bpcem(schDir, BoxMax, BoxMin, vec3(0, 12, 0));
  //schDir = schDir.xzy;
  vec3 shc = sphericalHarmonics(normalize(fs_in.Normal)) * u_scaleFactor;
  gLightAccumulation = shc;///max(CubeMapcolor.rgb * Metallic_Emmisive.r, vec3(0.0));
  gDepth.gba = CubeMapcolor.rgb;
  //gLightAccumulation = shc;
  //gWorldSpacePosition.rgb = fs_in.wFragPos;
  //gWorldSpacePosition.a = gl_FragCoord.z;
}
