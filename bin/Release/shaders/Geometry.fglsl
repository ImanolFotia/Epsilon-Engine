#version 440 core

layout(location = 0) out vec3 gAlbedoSpec;
layout(location = 1) out vec4 gExpensiveNormal;
layout(location = 2) out vec4 gDepth;
layout(location = 3) out vec4 gExtraComponents; 
layout(location = 4) out vec3 gLightAccumulation;
//layout(location = 5) out float gPosition;

layout (binding = 0) uniform sampler2D texture_diffuse;
layout (binding = 1) uniform sampler2D texture_specular;
layout (binding = 2) uniform sampler2D texture_normal;
layout (binding = 3) uniform sampler2D texture_height;
layout (binding = 4) uniform samplerCube skybox;

in VS_OUT{
  vec3 FragPos;
  vec3 Normal;
  vec2 TexCoords;
  mat3 TBN;
  mat3 TBN2;
  vec3 wFragPos;
} fs_in;


uniform vec3 viewPos;
uniform float time;
uniform int CubemapID;
uniform mat4 projection;
uniform bool parallaxOn;

float NEAR = 0.1;
float FAR = 3000.0;
float LinearizeDepth(float depth)
{
    float z = depth * 2.0 - 1.0; // Back to NDC
    return ((2.0 * NEAR * FAR) / (FAR + NEAR - z * (FAR - NEAR)));
}

float deLinearizeDepth(float depth){
    float nonLinearDepth = (FAR + NEAR - 2.0 * NEAR * FAR / depth) / (FAR - NEAR);
    nonLinearDepth = (nonLinearDepth + 1.0) / 2.0;
    return nonLinearDepth;
}

in vec4 PrevPosition;
in vec4 Position;


const float Pi = 3.141592654f;
const float CosineA0 = Pi;
const float CosineA1 = (2.0f * Pi) / 3.0f;
const float CosineA2 = Pi * 0.25f;
// scale for restored amount of lighting
uniform float u_scaleFactor = 0.25;

struct SphericalHarmonicsFormat {
  vec3 u_L00; float padding0;
  vec3 u_L1m1; float padding1;
  vec3 u_L10; float padding2;
  vec3 u_L11; float padding3;
  vec3 u_L2m2; float padding4;
  vec3 u_L2m1; float padding5;
  vec3 u_L20; float padding6;
  vec3 u_L21; float padding7;
  vec3 u_L22; float padding8;
};

layout(std430, binding = 1) buffer AmbientLightSSBO
{
    SphericalHarmonicsFormat ambientProbes;
/*
        vec3 u_L00[2]; float padding0;
        vec3 u_L1m1[2]; float padding1;
        vec3 u_L10[2]; float padding2;
        vec3 u_L11[2]; float padding3;
        vec3 u_L2m2[2]; float padding4;
        vec3 u_L2m1[2]; float padding5;
        vec3 u_L20[2]; float padding6;
        vec3 u_L21[2]; float padding7;
        vec3 u_L22[2]; float padding8;*/

} lp;

const float C1 = 0.429043;
const float C2 = 0.511664;
const float C3 = 0.743125;
const float C4 = 0.886227;
const float C5 = 0.247708;

vec3 sphericalHarmonics(vec3 N)
{
   return
      // band 0, constant value, details of lowest frequency
      C4 * lp.ambientProbes.u_L00 +

      // band 1, oriented along main axes
      2.0 * C2 * lp.ambientProbes.u_L11 * N.x +
      2.0 * C2 * lp.ambientProbes.u_L1m1 * N.y +
      2.0 * C2 * lp.ambientProbes.u_L10 * N.z +

      // band 2, values depend on multiple axes, higher frequency details
      C1 * lp.ambientProbes.u_L22 * (N.x * N.x - N.y * N.y) +
      C3 * lp.ambientProbes.u_L20 * N.z * N.z - C5 * lp.ambientProbes.u_L20 +
      2.0 * C1 * lp.ambientProbes.u_L2m2 * N.x * N.y +
      2.0 * C1 * lp.ambientProbes.u_L21 * N.x * N.z +
      2.0 * C1 * lp.ambientProbes.u_L2m1 * N.y * N.z;
}

int numSteps = 0;
float currentLayerDepth = 0.0;
float weight = 0.0;
float layerDepth = 0.0;
vec2 ParallaxMapping(vec2 texCoords, vec3 viewDir)
{ 
    // number of depth layers
    const float minLayers = 8;
    const float maxLayers = 32;
    float numLayers = mix(maxLayers, minLayers, abs(dot(vec3(0.0, 0.0, 1.0), viewDir)));  
    // calculate the size of each layer
    layerDepth = 1.0 / numLayers;
    // depth of current layer
    // the amount to shift the texture coordinates per layer (from vector P)
    vec2 P = viewDir.xy / viewDir.z * 0.08; 
    vec2 deltaTexCoords = P / numLayers;
  
    // get initial values
    vec2  currentTexCoords     = texCoords;
    float currentDepthMapValue = texture(texture_height, currentTexCoords).b;

    int maxSteps = 16;
      
    while(currentLayerDepth < currentDepthMapValue)
    {
        if(numSteps >= maxSteps)
          break;
        // shift texture coordinates along direction of P
        currentTexCoords -= deltaTexCoords;
        // get depthmap value at current texture coordinates
        currentDepthMapValue = texture(texture_height, currentTexCoords).b;  
        // get depth of next layer
        currentLayerDepth += layerDepth;  

        numSteps++;
    }
    
    // get texture coordinates before collision (reverse operations)
    vec2 prevTexCoords = currentTexCoords + deltaTexCoords;

    // get depth after and before collision for linear interpolation
    float afterDepth  = currentDepthMapValue - currentLayerDepth;
    float beforeDepth = (texture(texture_height, prevTexCoords).b) - currentLayerDepth + layerDepth;
 
    // interpolation of texture coordinates
    weight = afterDepth / (afterDepth - beforeDepth);
    vec2 finalTexCoords = prevTexCoords * weight + currentTexCoords * (1.0 - weight);

    return finalTexCoords;
}

vec3 bpcem (in vec3 v, vec3 Emax, vec3 Emin, vec3 Epos)
{ 
  vec3 nrdir = normalize(v);
  vec3 rbmax = (Emax - fs_in.wFragPos)/nrdir;
  vec3 rbmin = (Emin - fs_in.wFragPos)/nrdir;
  
  vec3 rbminmax;
  rbminmax.x = (nrdir.x>0.0)?rbmax.x:rbmin.x;
  rbminmax.y = (nrdir.y>0.0)?rbmax.y:rbmin.y;
  rbminmax.z = (nrdir.z>0.0)?rbmax.z:rbmin.z;   
  float fa = min(min(rbminmax.x, rbminmax.y), rbminmax.z);
  vec3 posonbox = fs_in.wFragPos + nrdir * fa;
  return posonbox - Epos;
}

vec3 BoxMax = vec3(-6.5, 12, 19.5);
vec3 BoxMin = vec3(-16, 0.5, -19);

uniform ivec2 Resolution;
uniform bool isTransparent = false;

float rand(vec2 n) { 
  return fract(sin(dot(n, vec2(12.9898, 4.1414))) * 43758.5453);
}
float noise(vec2 n) {
  const vec2 d = vec2(0.0, 1.0);
  vec2 b = floor(n), f = smoothstep(vec2(0.0), vec2(1.0), fract(n));
  return mix(mix(rand(b), rand(b + d.yx), f.x), mix(rand(b + d.xy), rand(b + d.yy), f.x), f.y);
}
float sum( vec3 v ) { return v.x+v.y+v.z; }
vec3 textureNoTile(in sampler2D tex, in vec2 x)
{
    float v = 0.5;
    float k = noise(x); // cheap (cache friendly) lookup
    
    float l = k*8.0;
    float i = floor( l );
    float f = fract( l );
    
    vec2 offa = sin(vec2(3.0,7.0)*(i+0.0)); // can replace with any other hash
    vec2 offb = sin(vec2(3.0,7.0)*(i+1.0)); // can replace with any other hash

    vec2 dx = dFdx(x), dy = dFdy(x);
    
    vec3 cola = textureGrad( tex, x + v*offa, dx, dy ).xyz;
    vec3 colb = textureGrad( tex, x + v*offb, dx, dy ).xyz;
    
    return mix( cola, colb, smoothstep(0.2,0.8,f-0.1*sum(cola-colb)) );
}

mat3 cotangent_frame(vec3 N, vec3 p, vec2 uv)
{
    // get edge vectors of the pixel triangle
    vec3 dp1 = dFdx( p );
    vec3 dp2 = dFdy( p );
    vec2 duv1 = dFdx( uv );
    vec2 duv2 = dFdy( uv );
 
    // solve the linear system
    vec3 dp2perp = cross( dp2, N );
    vec3 dp1perp = cross( N, dp1 );
    vec3 T = dp2perp * duv1.x + dp1perp * duv2.x;
    vec3 B = dp2perp * duv1.y + dp1perp * duv2.y;
 
    // construct a scale-invariant frame 
    float invmax = inversesqrt( max( dot(T,T), dot(B,B) ) );
    return mat3( T * invmax, B * invmax, N );
}

vec3 perturb_normal( vec3 N, vec3 V, vec2 texcoord )
{
    // assume N, the interpolated vertex normal and 
    // V, the view vector (vertex to eye)
   vec3 map = texture(texture_normal, texcoord ).xyz;
   map = map * 255./127. - 128./127.;
    mat3 TBN = cotangent_frame(N, -V, texcoord);
    return normalize(TBN * map);
}

const float threshold[16] = float[16](
    1./16., 9./16., 3./16., 11./16., 
    13./16., 5./16., 15./16., 7./16., 
    4./16., 12./16., 2./16., 10./16.,
    16./16., 8./16., 14./16., 6./16.);

float ScreenDoor(int x, int y, float v)
{
    return step(threshold[4*y+x],v);
}

void main()
{

  ivec2 invFragCoord = ivec2(gl_FragCoord);///ivec2(fragCoord);  
  int indexX = int(mod(int(invFragCoord.x), 4)); 
  int indexY = int(mod(int(invFragCoord.y), 4));

  if(ScreenDoor(indexX, indexY, texture(texture_diffuse,fs_in.TexCoords).a) < 1.0)
    discard;  

    vec3 Dir = normalize((fs_in.TBN * viewPos) - (fs_in.TBN * fs_in.wFragPos));


  vec2 texCoords;  
  if(texture(texture_height, fs_in.TexCoords).b > 0.0 && parallaxOn)
    texCoords = ParallaxMapping(fs_in.TexCoords, Dir);
  else
    texCoords = fs_in.TexCoords;


  //vec3 exNormal = normalize(perturb_normal(fs_in.Normal, normalize(fs_in.wFragPos - viewPos), texCoords));

  int negator = ( int(gl_FrontFacing) * 2 - 1 );
  gExpensiveNormal.rgb = negator * normalize( normalize( texture(texture_normal, texCoords).rgb * 2.0 - 1.0) * fs_in.TBN);

  gExpensiveNormal.rgb = clamp(gExpensiveNormal.rgb, -1.0, 1.0);
  //gExpensiveNormal.rgb = vec3(gExpensiveNormal.x, -gExpensiveNormal.y, gExpensiveNormal.z);
  float Spec = texture(texture_specular, texCoords).x;
  float Wet = textureNoTile(texture_specular, texCoords).y;
  gExpensiveNormal.a = max(Spec/* - Wet*/, 0.0);

  vec4 CubeMapcolor = vec4(0.0);
  vec3 R = vec3(0.0);
  gAlbedoSpec.rgb = texture(texture_diffuse,texCoords).rgb;
  
  float bias = max(0.01 * (1.0 - dot(gExpensiveNormal.rgb, Dir)), 0.5) * texture(texture_height, texCoords).b;
  //gDepth.r = gl_FragCoord.z;
  //gPosition = fs_in.FragPos.z;
  vec2 MetallicHeight = texture(texture_height, texCoords).rg;
  gExtraComponents.x = min(MetallicHeight.x/* + Wet*/, 1.0); 
  gExtraComponents.y = MetallicHeight.y; 
  vec2 a = (Position.xy / Position.w) * 0.5 + 0.5;
  vec2 b = (PrevPosition.xy / PrevPosition.w) * 0.5 + 0.5;

  gExtraComponents.zw = vec2(a - b);
  const float MAX_REFLECTION_LOD = 4.0;
    //vec3 prefilteredColor = textureLod(skybox, R,  roughness * MAX_REFLECTION_LOD).rgb;    
    //vec2 brdf  = texture(brdfLUT, vec2(max(dot(N, V), 0.0), roughness)).rg;
    //vec3 specular = prefilteredColor * (F /** brdf.x + brdf.y*/);

  CubeMapcolor = textureLod(skybox, bpcem(
                                      reflect(normalize(fs_in.wFragPos - viewPos), normalize(gExpensiveNormal.rgb)), 
                                      BoxMax, BoxMin, vec3(-11.0, 6.0, -8.0)), gExpensiveNormal.a * MAX_REFLECTION_LOD);

  vec3 shc = sphericalHarmonics(normalize(gExpensiveNormal.rgb)) * u_scaleFactor;

  gLightAccumulation = shc;
  gDepth = vec4(gl_FragCoord.z, CubeMapcolor.rgb);
}
